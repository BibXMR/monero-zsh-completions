#compdef monerod

_monerod() {
  local state

  local -a daemon_commands
  daemon_commands=(
    'help:Show the help section or the documentation about a <command>'
    'apropos:Search all command descriptions for keyword(s)'
    'print_height:Print the local blockchain height'
    'print_pl:Print the current peer list'
    'print_pl_stats:Print the peer list statistics'
    'print_cn:Print the current connections'
    'print_net_stats:Print network statistics'
    'print_bc:Print the blockchain info in a given blocks range'
    'print_block:Print a given block'
    'print_tx:Print a given transaction'
    'is_key_image_spent:Print whether a given key image is in the spent key images set'
    'start_mining:Start mining for specified address'
    'stop_mining:Stop mining'
    'mining_status:Show current mining status'
    'print_pool:Print the transaction pool using a long format'
    'print_pool_sh:Print transaction pool using a short format'
    'print_pool_stats:Print the transaction pool'\''s statistics'
    'show_hr:Start showing the current hash rate'
    'hide_hr:Stop showing the hash rate'
    'save:Save the blockchain'
    'set_log:Change the current log level/categories where <level> is a number 0-4'
    'diff:Show the current difficulty'
    'status:Show the current status'
    'stop_daemon:Stop the daemon'
    'exit:Stop the daemon'
    'print_status:Print the current daemon status'
    'limit:Get or set the download and upload limit'
    'limit_up:Get or set the upload limit'
    'limit_down:Get or set the download limit'
    'out_peers:Set the <max_number> of out peers'
    'in_peers:Set the <max_number> of in peers'
    'hard_fork_info:Print the hard fork voting information'
    'bans:Show the currently banned IPs'
    'ban:Ban a given <IP> or list of IPs from a file for a given amount of <seconds>'
    'unban:Unban a given <IP>'
    'banned:Check whether an <address> is banned'
    'flush_txpool:Flush a transaction from the tx pool by its <txid>, or the whole tx pool'
    'output_histogram:Print the output histogram of outputs'
    'print_coinbase_tx_sum:Print the sum of coinbase transactions'
    'alt_chain_info:Print the information about alternative chains'
    'bc_dyn_stats:Print the information about current blockchain dynamic state'
    'update:Check if an update is available, optionally downloads it if there is'
    'relay_tx:Relay a given transaction by its <txid>'
    'sync_info:Print information about the blockchain sync state'
    'pop_blocks:Remove blocks from end of blockchain'
    'rpc_payments:Print information about RPC payments'
    'version:Print version information'
    'prune_blockchain:Prune the blockchain'
    'check_blockchain_pruning:Check the blockchain pruning'
    'set_bootstrap_daemon:Use '\''auto'\'' to enable automatic public nodes discovering and bootstrap daemon switching'
    'flush_cache:Flush the specified cache(s)'
  )

  _arguments \
    '(- *)--help[Produce help message]' \
    '(- *)--version[Output version information]' \
    '*--add-exclusive-node=[Specify list of peers to connect to only.]:peer (ip\:port)' \
    '*--add-peer=[Manually add peer to local peerlist]:peer (ip\:port)' \
    '*--add-priority-node=[Specify list of peers to connect to and attempt to keep the connection open]:peer (ip\:port)' \
    '--allow-local-ip[Allow local ip add to peer list, mostly in debug purposes]' \
    '*--anonymous-inbound=[<hidden-service-address>,<\[bind-ip\:\]port>\[,max_connections\] i.e.]:inbound config' \
    '--ban-list=[Specify ban list file, one IP address per line]:ban list file:_files' \
    '--batch-max-weight=[How many megabytes to sync in one batch during chain synchronization, default is 10]:megabytes' \
    '--bg-mining-enable[enable background mining]' \
    '--bg-mining-idle-threshold=[Specify minimum avg idle percentage over lookback interval]:percentage' \
    '--bg-mining-ignore-battery[if true, assumes plugged in when unable to query system power status]' \
    '--bg-mining-min-idle-interval=[Specify min lookback interval in seconds for determining idle state]:seconds' \
    '--bg-mining-miner-target=[Specify maximum percentage cpu use by miner\(s\)]:percentage' \
    '--block-download-max-size=[Set maximum size of block download queue in bytes \(0 for default\)]:size (bytes)' \
    '--block-notify=[Run a program for each new block, '\''%s'\'' will be replaced by the block hash]:command' \
    '--block-rate-notify=[Run a program when the block rate undergoes large fluctuations.]:command' \
    '--block-sync-size=[How many blocks to sync at once during chain synchronization \(0 = adaptive\).]:blocks' \
    '--bootstrap-daemon-address=[URL of a '\''bootstrap'\'' remote daemon that the connected wallets can use while this daemon is still not fully synced.\nUse '\''auto'\'' to enable automatic public nodes discovering and bootstrap daemon switching]:URL' \
    '--bootstrap-daemon-login=[Specify username\:password for the bootstrap daemon login]:credentials (user\:pass)' \
    '--bootstrap-daemon-proxy=[<ip>\:<port> socks proxy to use for bootstrap daemon connections]:proxy (ip\:port)' \
    '--check-updates=[Check for new versions of monero\: \[disabled|notify|download|update\]]:check-updates:(disabled notify download update)' \
    '--config-file=[Specify configuration file]:config file:_files' \
    '--confirm-external-bind[Confirm rpc-bind-ip value is NOT a loopback \(local\) IP]' \
    '--data-dir=[Specify data directory]:data directory:_directories' \
    '--db-salvage[Try to salvage a blockchain database if it seems corrupted]' \
    '--db-sync-mode=[Specify sync option, using format \[safe|fast|fastest\]\:\[sync|async\]\:\[<nblocks_per_sync>\[blocks\]|<nbytes_per_sync>\[bytes\]\].]:mode' \
    '--detach[Run as daemon]' \
    '--disable-dns-checkpoints[Do not retrieve checkpoints from DNS]' \
    '--disable-rpc-ban[Do not ban hosts on RPC errors]' \
    '--enable-dns-blocklist[Apply realtime blocklist from DNS]' \
    '--enforce-dns-checkpointing[checkpoints from DNS server will be enforced]' \
    '--extra-messages-file=[Specify file for extra messages to include into coinbase transactions]:file:_files' \
    '--fast-block-sync=[Sync up most of the way by using embedded, known block hashes.]:enabled (1 or 0)' \
    '--fixed-difficulty=[Fixed difficulty used for testing.]:difficulty' \
    '--hide-my-port[Do not announce yourself as peerlist candidate]' \
    '--igd=[UPnP port mapping \(disabled, enabled, delayed\)]:igd:(disabled enabled delayed)' \
    '--in-peers=[set max number of in peers]:count' \
    '--keep-alt-blocks[Keep alternative blocks on restart]' \
    '--keep-fakechain[Don'\''t delete any existing database when in fakechain mode.]' \
    '--limit-rate=[set limit-rate \[kB/s\]]:rate (kB/s)' \
    '--limit-rate-down=[set limit-rate-down \[kB/s\]]:rate (kB/s)' \
    '--limit-rate-up=[set limit-rate-up \[kB/s\]]:rate (kB/s)' \
    '--log-file=[Specify log file]:log file:_files' \
    '--log-level=[0-4 or categories]:log level' \
    '--max-concurrency=[Max number of threads to use for a parallel job]:threads' \
    '--max-connections-per-ip=[Maximum number of p2p connections allowed from the same IP address]:count' \
    '--max-log-file-size=[Specify maximum log file size \[B\]]:size (bytes)' \
    '--max-log-files=[Specify maximum number of rotated log files to be saved \(no limit by setting to 0\)]:count' \
    '--max-txpool-weight=[Set maximum txpool weight in bytes.]:weight (bytes)' \
    '--mining-threads=[Specify mining threads count]:threads' \
    '--no-igd[Disable UPnP port mapping]' \
    '--no-sync[Don'\''t synchronize the blockchain with other peers]' \
    '--no-zmq[Disable ZMQ RPC server]' \
    '--non-interactive[Run non-interactive]' \
    '--offline[Do not listen for peers, nor connect to any]' \
    '--out-peers=[set max number of out peers]:count' \
    '--p2p-bind-ip=[Interface for p2p network protocol \(IPv4\)]:IP address' \
    '--p2p-bind-ipv6-address=[Interface for p2p network protocol \(IPv6\)]:IPv6 address' \
    '--p2p-bind-port=[Port for p2p network protocol \(IPv4\)]:port' \
    '--p2p-bind-port-ipv6=[Port for p2p network protocol \(IPv6\)]:port' \
    '--p2p-external-port=[External port for p2p network protocol \(if port forwarding used with NAT\)]:port' \
    '--p2p-ignore-ipv4[Ignore unsuccessful IPv4 bind for p2p]' \
    '--p2p-use-ipv6[Enable IPv6 for p2p]' \
    '--pad-transactions[Pad relayed transactions to help defend against traffic volume analysis]' \
    '--pidfile=[File path to write the daemon'\''s PID to \(optional, requires --detach\)]:pid file:_files' \
    '--prep-blocks-threads=[Max number of threads to use when preparing block hashes in groups.]:threads' \
    '--proxy=[Network communication through proxy\: <socks-ip\:port> i.e.]:proxy (ip\:port)' \
    '--proxy-allow-dns-leaks[Allow DNS leaks outside of proxy]' \
    '--prune-blockchain[Prune blockchain]' \
    '--public-node[Allow other users to use the node as a remote \(restricted RPC mode, view-only commands\) and advertise it over P2P]' \
    '(--testnet --stagenet)--regtest[Run in a regression testing mode.]' \
    '--reorg-notify=[Run a program for each reorg, '\''%s'\'' will be replaced by the split height, '\''%h'\'' will be replaced by the new blockchain height, '\''%n'\'' will be replaced by the number of new blocks in the new chain, and '\''%d'\'' will be replaced by the number of blocks discarded from the old chain]:command' \
    '--restricted-rpc[Restrict RPC to view only commands and do not return privacy sensitive data in RPC calls]' \
    '--rpc-access-control-origins=[Specify a comma separated list of origins to allow cross origin resource sharing]:origins' \
    '--rpc-bind-ip=[Specify IP to bind RPC server]:IP address' \
    '--rpc-bind-ipv6-address=[Specify IPv6 address to bind RPC server]:IPv6 address' \
    '--rpc-bind-port=[Port for RPC server]:port' \
    '--rpc-ignore-ipv4[Ignore unsuccessful IPv4 bind for RPC]' \
    '--rpc-login=[Specify username\[\:password\] required for RPC server]:credentials (user\:pass)' \
    '--rpc-max-connections=[Max RPC connections permitted]:count' \
    '--rpc-max-connections-per-private-ip=[Max RPC connections per private and localhost IP permitted]:count' \
    '--rpc-max-connections-per-public-ip=[Max RPC connections per public IP permitted]:count' \
    '--rpc-payment-address=[Restrict RPC to clients sending micropayment to this address]:Monero address' \
    '--rpc-payment-allow-free-loopback[Allow free access from the loopback address \(ie, the local host\)]' \
    '--rpc-payment-credits=[Restrict RPC to clients sending micropayment, yields that many credits per payment]:credits' \
    '--rpc-payment-difficulty=[Restrict RPC to clients sending micropayment at this difficulty]:difficulty' \
    '--rpc-response-soft-limit=[Max response bytes that can be queued, enforced at next response attempt]:bytes' \
    '--rpc-restricted-bind-ip=[Specify IP to bind restricted RPC server]:IP address' \
    '--rpc-restricted-bind-ipv6-address=[Specify IPv6 address to bind restricted RPC server]:IPv6 address' \
    '--rpc-restricted-bind-port=[Port for restricted RPC server]:port' \
    '--rpc-ssl=[Enable SSL on RPC connections\: enabled|disabled|autodetect]:rpc-ssl:(enabled disabled autodetect)' \
    '--rpc-ssl-allow-any-cert[Allow any peer certificate]' \
    '--rpc-ssl-allow-chained[Allow user \(via --rpc-ssl-certificates\) chain certificates]' \
    '*--rpc-ssl-allowed-fingerprints=[List of certificate fingerprints to allow]:fingerprint' \
    '--rpc-ssl-ca-certificates=[Path to file containing concatenated PEM format certificate\(s\) to replace system CA\(s\).]:rpc-ssl-ca-certificates:_files' \
    '--rpc-ssl-certificate=[Path to a PEM format certificate]:rpc-ssl-certificate:_files' \
    '--rpc-ssl-private-key=[Path to a PEM format private key]:rpc-ssl-private-key:_files' \
    '--rpc-use-ipv6[Allow IPv6 for RPC]' \
    '*--seed-node=[Connect to a node to retrieve peer addresses, and disconnect]:peer (ip\:port)' \
    '--show-time-stats=[Show time-stats when processing blocks/txs and disk synchronization.]:enabled (1 or 0)' \
    '--span-limit=[Defines how many minutes of block synchronization data to request at a time \(default is 2 minutes\)]:minutes' \
    '(--testnet --regtest)--stagenet[Run on stagenet.]' \
    '--start-mining=[Specify wallet address to mining for]:Monero address' \
    '--sync-pruned-blocks[Allow syncing from nodes with only pruned blocks]' \
    '--test-dbg-lock-sleep=[Sleep time in ms, defaults to 0 \(off\), used to debug before/after locking mutex.]:time (ms)' \
    '--test-drop-download[For net tests\: in download, discard ALL blocks instead checking/saving them \(very fast\)]' \
    '--test-drop-download-height=[Like test-drop-download but discards only after around certain height]:height' \
    '(--stagenet --regtest)--testnet[Run on testnet.]' \
    '--tos-flag=[set TOS flag]:flag' \
    '*--tx-proxy=[Send local txes through proxy\: <network-type>,<socks-ip\:port>\[,max_connections\]\[,disable_noise\] i.e.]:proxy config' \
    '*--zmq-pub=[Address for ZMQ pub - tcp\://ip\:port or ipc\://path]:address' \
    '--zmq-rpc-bind-ip=[IP for ZMQ RPC server to listen on]:IP address' \
    '--zmq-rpc-bind-port=[Port for ZMQ RPC server to listen on]:port' \
    '*::daemon command:->command'

  case $state in
    command)
      if (( CURRENT == 1 )); then
        _describe -t commands "daemon command" daemon_commands
      fi
    ;;
  esac
}

_monerod "$@"
