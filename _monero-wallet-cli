#compdef monero-wallet-cli

_monero-wallet-cli() {
  local state

  local -a wallet_commands
  wallet_commands=(
    'help:Show the help section or the documentation about a <command>'
    'apropos:Search all command descriptions for keyword(s)'
    'start_mining:Start mining in the daemon (bg_mining and ignore_battery are optional booleans)'
    'stop_mining:Stop mining in the daemon'
    'set_daemon:Set another daemon to connect to'
    'save_bc:Save the current blockchain data'
    'refresh:Synchronize the transactions and balance'
    'balance:Show the wallet'\''s balance of the currently selected account'
    'incoming_transfers:Amount, Spent(\"T\"|\"F\"), \"frozen\"|\"locked\"|\"unlocked\", RingCT, Global Index, Transaction Hash, Address Index, [Public Key, Key Image]'
    'payments:Show the payments for the given payment IDs'
    'bc_height:Show the blockchain height'
    'transfer:Transfer <address> <amount>'
    'sweep_unmixable:Send all unmixable outputs to yourself with ring_size 1'
    'sweep_all:Send all unlocked balance to an address'
    'sweep_account:Send all unlocked balance from a given account to an address'
    'sweep_below:Send all unlocked outputs below the threshold to an address'
    'sweep_single:Send a single output of the given key image to an address without change'
    'donate:Donate <amount> to the development team (donate.getmonero.org)'
    'sign_transfer:Use the parameter <filename> to specify the file to read from'
    'submit_transfer:Submit a signed transaction from a file'
    'set_log:Change the current log detail (level must be <0-4>)'
    'account:If the \"tag_description\" argument is specified, the tag <tag_name> is assigned an arbitrary text <description>'
    'address:If no arguments are specified or <index> is specified, the wallet shows the default or specified address'
    'integrated_address:Encode a payment ID into an integrated address for the current wallet public address (no argument uses a random payment ID), or decode an integrated address to standard address and payment ID'
    'address_book:Print all entries in the address book, optionally adding/deleting an entry to/from it'
    'save:Save the wallet data'
    'save_watch_only:Save a watch-only keys file'
    'viewkey:Display the private view key'
    'spendkey:Display the private spend key'
    'seed:Display the Electrum-style mnemonic seed'
    'restore_height:Display the restore height'
    'set:How many seconds to wait before locking the wallet (0 to disable)'
    'encrypted_seed:Display the encrypted Electrum-style mnemonic seed'
    'rescan_spent:Rescan the blockchain for spent outputs'
    'get_tx_key:Get the transaction key (r) for a given <txid>'
    'set_tx_key:Set the transaction key (r) for a given <txid> in case the tx was made by some other device or 3rd party wallet'
    'check_tx_key:Check the amount going to <address> in <txid>'
    'get_tx_proof:Generate a signature proving funds sent to <address> in <txid>, optionally with a challenge string <message>, using either the transaction secret key (when <address> is not your wallet'\''s address) or the view secret key (otherwise), which does not disclose the secret key'
    'check_tx_proof:Check the proof for funds going to <address> in <txid> with the challenge string <message> if any'
    'get_spend_proof:Generate a signature proving that you generated <txid> using the spend secret key, optionally with a challenge string <message>'
    'check_spend_proof:Check a signature proving that the signer generated <txid>, optionally with a challenge string <message>'
    'get_reserve_proof:Otherwise, you prove the reserve of the smallest possible amount above <amount> available in your current account'
    'check_reserve_proof:Check a signature proving that the owner of <address> holds at least this much, optionally with a challenge string <message>'
    'show_transfers:** Set of address indices used as inputs in this transfer'
    'export_transfers:Export to CSV the incoming/outgoing transfers within an optional height range'
    'unspent_outputs:Show the unspent outputs of a specified address within an optional amount range'
    'rescan_bc:Rescan the blockchain from scratch'
    'set_tx_note:Set an arbitrary string note for a <txid>'
    'get_tx_note:Get a string note for a txid'
    'set_description:Set an arbitrary description for the wallet'
    'get_description:Get the description of the wallet'
    'status:Show the wallet'\''s status'
    'wallet_info:Show the wallet'\''s information'
    'sign:Sign the contents of a file with the given subaddress (or the main address if not specified)'
    'verify:Verify a signature on the contents of a file'
    'export_key_images:Export a signed set of key images to a <filename>'
    'import_key_images:Import a signed key images list and verify their spent status'
    'hw_key_images_sync:Synchronizes key images with the hw wallet'
    'hw_reconnect:Attempts to reconnect HW wallet'
    'export_outputs:Export a set of outputs owned by this wallet'
    'import_outputs:Import a set of outputs owned by this wallet'
    'show_transfer:Show information about a transfer to/from this address'
    'password:Change the wallet'\''s password'
    'payment_id:Generate a new random full size payment id (obsolete)'
    'fee:Print the information about the current fee and transaction backlog'
    'prepare_multisig:Export data needed to create a multisig wallet'
    'make_multisig:Turn this wallet into a multisig wallet'
    'exchange_multisig_keys:Performs extra multisig keys exchange rounds'
    'export_multisig_info:Export multisig info for other participants'
    'import_multisig_info:Import multisig info from other participants'
    'sign_multisig:Sign a multisig transaction from a file'
    'submit_multisig:Submit a signed multisig transaction from a file'
    'export_raw_multisig_tx:Export a signed multisig transaction to a file'
    'mms:Get help about a subcommand with: help mms <subcommand>, or help mms <subcommand>'
    'print_ring:Key Image, \"absolute\", list of rings'
    'set_ring:Set the ring used for a given key image, so it can be reused in a fork'
    'unset_ring:Unsets the ring used for a given key image or transaction'
    'save_known_rings:Save known rings to the shared rings database'
    'mark_output_spent:Mark output(s) as spent so they never get selected as fake outputs in a ring'
    'mark_output_unspent:Marks an output as unspent so it may get selected as a fake output in a ring'
    'is_output_spent:Checks whether an output is marked as spent'
    'freeze:Freeze a single output by key image so it will not be used'
    'thaw:Thaw a single output by key image so it may be used again'
    'frozen:Checks whether a given output is currently frozen by key image'
    'lock:Lock the wallet console, requiring the wallet password to continue'
    'net_stats:Prints simple network stats'
    'public_nodes:Lists known public nodes'
    'welcome:Prints basic info about Monero for first time users'
    'version:Returns version information'
    'show_qr_code:Show address as QR code'
    'scan_tx:Scan the transactions given by <txid>(s), processing them and looking for outputs'
  )

  _arguments \
    '(- *)--help[Produce help message]' \
    '(- *)--version[Output version information]' \
    '--allow-mismatched-daemon-version[Allow communicating with a daemon that uses a different version]' \
    '--bitmessage-address=[Use PyBitmessage instance at URL <arg>]:URL' \
    '--bitmessage-login=[Specify <arg> as username\:password for PyBitmessage API]:credentials (user\:pass)' \
    '--config-file=[Config file]:config file:_files' \
    '--create-address-file[Create an address file for new wallets]' \
    '--daemon-address=[Use daemon instance at <host>\:<port>]:daemon address (host\:port)' \
    '--daemon-host=[Use daemon instance at host <arg> instead of localhost]:hostname' \
    '--daemon-login=[Specify username\[\:password\] for daemon RPC client]:credentials (user\:pass)' \
    '--daemon-port=[Use daemon instance at port <arg> instead of 18081]:port' \
    '--daemon-ssl=[Enable SSL on daemon RPC connections\: enabled|disabled|autodetect]:daemon-ssl:(enabled disabled autodetect)' \
    '--daemon-ssl-allow-any-cert[Allow any SSL certificate from the daemon]' \
    '--daemon-ssl-allow-chained[Allow user \(via --daemon-ssl-ca-certificates\) chain certificates]' \
    '*--daemon-ssl-allowed-fingerprints=[List of valid fingerprints of allowed RPC servers]:fingerprint' \
    '--daemon-ssl-ca-certificates=[Path to file containing concatenated PEM format certificate\(s\) to replace system CA\(s\).]:daemon-ssl-ca-certificates:_files' \
    '--daemon-ssl-certificate=[Path to a PEM format certificate]:daemon-ssl-certificate:_files' \
    '--daemon-ssl-private-key=[Path to a PEM format private key]:daemon-ssl-private-key:_files' \
    '--do-not-relay[The newly created transaction will not be relayed to the monero network]' \
    '--electrum-seed=[Specify Electrum seed for wallet recovery/creation]:mnemonic seed' \
    '--extra-entropy=[File containing extra entropy to initialize the PRNG \(any data, aim for 256 bits of entropy to be useful, which typically means more than 256 bits of data\)]:entropy file:_files' \
    '--generate-from-device=[Generate new wallet from device and save it to <arg>]:wallet file:_files' \
    '--generate-from-json=[Generate wallet from JSON format file]:JSON file:_files' \
    '--generate-from-keys=[Generate wallet from private keys]:wallet file:_files' \
    '--generate-from-multisig-keys=[Generate a master wallet from multisig wallet keys]:wallet file:_files' \
    '--generate-from-spend-key=[Generate deterministic wallet from spend key]:wallet file:_files' \
    '--generate-from-view-key=[Generate incoming-only wallet from view key]:wallet file:_files' \
    '--generate-new-wallet=[Generate new wallet and save it to <arg>]:wallet file:_files' \
    '--hw-device=[HW device to use]:device' \
    '--hw-device-deriv-path=[HW device wallet derivation path \(e.g., SLIP-10\)]:derivation path' \
    '--kdf-rounds=[Number of rounds for the key derivation function]:rounds' \
    '--log-file=[Specify log file]:log file:_files' \
    '--log-level=[0-4 or categories]:log level' \
    '--max-concurrency=[Max number of threads to use for a parallel job]:threads' \
    '--max-log-file-size=[Specify maximum log file size \[B\]]:size (bytes)' \
    '--max-log-files=[Specify maximum number of rotated log files to be saved \(no limit by setting to 0\)]:count' \
    '--mnemonic-language=[Language for mnemonic]:language' \
    '--no-dns[Do not use DNS]' \
    '--non-deterministic[Generate non-deterministic view and spend keys]' \
    '--offline[Do not connect to a daemon, nor use DNS]' \
    '--password=[Wallet password \(escape/quote as needed\)]:password' \
    '--password-file=[Wallet password file]:password file:_files' \
    '--proxy=[\[<ip>\:\]<port> socks proxy to use for daemon connections]:proxy (ip\:port)' \
    '--restore-date=[Restore from estimated blockchain height on specified date]:date (YYYY-MM-DD)' \
    '--restore-deterministic-wallet[Recover wallet using Electrum-style mnemonic seed]' \
    '--restore-from-seed[alias for --restore-deterministic-wallet]' \
    '--restore-height=[Restore from specific blockchain height]:block height' \
    '--restore-multisig-wallet[Recover multisig wallet using Electrum-style mnemonic seed]' \
    '--shared-ringdb-dir=[Set shared ring database path]:directory:_directories' \
    '(--testnet)--stagenet[For stagenet.]' \
    '--subaddress-lookahead=[Set subaddress lookahead sizes to <major>\:<minor>]:lookahead (major\:minor)' \
    '(--stagenet)--testnet[For testnet.]' \
    '--trusted-daemon[Enable commands which rely on a trusted daemon]' \
    '--tx-notify=[Run a program for each new incoming transaction, '\''%s'\'' will be replaced by the transaction hash]:command' \
    '--untrusted-daemon[Disable commands which rely on a trusted daemon]' \
    '--use-english-language-names[Display English language names]' \
    '--wallet-file=[Use wallet <arg>]:wallet file:_files' \
    '*::wallet command:->command'

  case $state in
    command)
      if (( CURRENT == 1 )); then
        _describe -t commands "wallet command" wallet_commands
      fi
    ;;
  esac
}

_monero-wallet-cli "$@"
